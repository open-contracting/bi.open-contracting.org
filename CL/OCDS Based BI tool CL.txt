///$tab Main
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='$#,##0.00;-$#,##0.00';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD.MM.YYYY';
SET TimestampFormat='DD.MM.YYYY hh:mm:ss[.fff]';
SET FirstWeekDay=0;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET CreateSearchIndexOnReload=1;
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:μ;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';

SET HidePrefix = '_';
///$tab 00. Parameters
REM Select environment (for data connection names)
	0 = dev
    1 = test
    2 = prod
;

SET scrEnvironment = 2;

//Languages
Set scrPrefix = '_';
Set scrLan1Name = 'En';	//English
Set scrLan2Name = 'Es'; //Spanish

//Currency
Set scrCurrency1Name = 'USD';
Set scrCurrency2Name = 'CLP';
// Set scrCurrency3Name = 'CLP';

//Paths to Multilanguage source

SET scrMultilangURL		= '1hpIYuT44x67Q-D2c6DnE_2QPefNcKEsKtubI0LZ9fEU';
SET scrMultiLangInner	= '556587033';
SET	scrMultilangFields	= '1797983248';
SET scrMultilangTransl	= '1332108298';
SET scrMultilangHelp	= '1120721356';


IF scrEnvironment = 0 THEN 

    // Paths to local Data //dev
	SET scr_DC_path = 'lib://root - karandin (rbcgrp_karandin)/karandin/OCDS CL';
    
    //Web Connection Name //dev
    SET scrURLConnectionName = 'lib://karandin_test_CDB3_timberSpecies (rbcgrp_karandin)';

ELSEIF scrEnvironment = 1 THEN 

    // Paths to local Data //test
	SET scr_DC_path = 'lib://OCDSint/Chile';
    
    //Web Connection Name //test
    SET scrURLConnectionName = 'lib://WorldPopulation';
    
ELSEIF scrEnvironment = 2 THEN

    // Paths to local Data //prod
	SET scr_DC_path = 'lib://OCDSint/Chile';

    //Web Connection Name //prod
    SET scrURLConnectionName = 'lib://WorldPopulation';
    
ENDIF;

REM There are 3 different sources for open, direct and frameworkAgreement procedures;

// //Open

// LET scrDIC_QVD_PATH_OP 			= scr_DC_path&'/Open/01_QVDDB/01_Dictionary/';
// LET scrTRN_QVD_PATH_OP			= scr_DC_path&'/Open/01_QVDDB/02_Transaction/';
// // LET scrLOCAL_DATA_PATH_OP 		= scr_DC_path&'/Open/01_QVDDB/03_LocalData/';
// // LET scrLOCAL_FILES_PATH_OP 		= scr_DC_path&'/Open/02_LocalFiles/';

// //Direct

// LET scrDIC_QVD_PATH_DR 			= scr_DC_path&'/Direct/01_QVDDB/01_Dictionary/';
// LET scrTRN_QVD_PATH_DR			= scr_DC_path&'/Direct/01_QVDDB/02_Transaction/';
// // LET scrLOCAL_DATA_PATH_DR 		= scr_DC_path&'/Direct/01_QVDDB/03_LocalData/';
// // LET scrLOCAL_FILES_PATH_DR 		= scr_DC_path&'/Direct/02_LocalFiles/';

// //FrameworkAgreements

// LET scrDIC_QVD_PATH_FA 			= scr_DC_path&'/FrameworkAgreement/01_QVDDB/01_Dictionary/';
// LET scrTRN_QVD_PATH_FA			= scr_DC_path&'/FrameworkAgreement/01_QVDDB/02_Transaction/';
// // LET scrLOCAL_DATA_PATH_FA 		= scr_DC_path&'/FrameworkAgreement/01_QVDDB/03_LocalData/';
// // LET scrLOCAL_FILES_PATH_FA 		= scr_DC_path&'/FrameworkAgreement/02_LocalFiles/';

//Global

LET scrDIC_QVD_PATH_GB 			= scr_DC_path&'/Global/01_QVDDB/01_Dictionary/';
LET scrTRN_QVD_PATH_GB 			= scr_DC_path&'/Global/01_QVDDB/02_Transaction/';
LET scrLOCAL_DATA_PATH 			= scr_DC_path&'/Global/01_QVDDB/03_LocalData/';
LET scrLOCAL_FILES_PATH 		= scr_DC_path&'/Global/02_LocalFiles/';

//Model

LET scrQVD_PATH_MD 				= scr_DC_path&'/Model/01_QVDDB/';
///$tab 01. Procedures
// Translation procedures
SUB translationsNewTable(newTableName, existingTableName, keyField)
// 0 - w/o KEEP
  FieldNamesTmp:
  NoConcatenate
  Load 
      "Index",
      [$(scrLan1Name)],
      [$(scrLan2Name)]
  Resident FieldNamesTable
  Where Index Like '$(newTableName)';
  
  TRACE $(tableName);

  Let scrLan1FieldName = Peek('$(scrLan1Name)', -1, 'FieldNamesTmp');
  Let scrLan2FieldName = Peek('$(scrLan2Name)', -1, 'FieldNamesTmp');

  IF existingTableName <> 0 THEN
    [$(newTableName)]:
    Left Keep("$(existingTableName)")
    Load 
        "Index" as [$(keyField)],
        [$(scrLan1Name)] as [$(scrLan1FieldName)],
        [$(scrLan2Name)] as [$(scrLan2FieldName)]
    Resident InnerDictionaries
    Where attributeOf Like '$(newTableName)';
    
    ELSEIF existingTableName = 0 THEN
    	[$(newTableName)]:
    	Load 
            "Index" as [$(keyField)],
            [$(scrLan1Name)] as [$(scrLan1FieldName)],
        	[$(scrLan2Name)] as [$(scrLan2FieldName)]
    	Resident InnerDictionaries
    	Where attributeOf Like '$(newTableName)';
   ENDIF

  drop table FieldNamesTmp;
ENDSUB

//KPI and Dims translations
SUB translationsJoinInners(tableName, keyField)
  JoinTypes:
  Load Distinct 
  		Reference as joinType
  Resident InnerDictionaries
  Where attributeOf Like '$(tableName)';

  FOR Each scrJoinType in FieldValueList('joinType')
    Left Join("$(tableName)")
    Load 
    	 Index as [$(keyField)],
         [$(scrLan1Name)] as "$(scrPrefix)$(tableName)$(vJoinType)$(scrLan1Name)",
         [$(scrLan2Name)] as "$(scrPrefix)$(tableName)$(vJoinType)$(scrLan2Name)"
	Resident InnerDictionaries
    Where attributeOf Like '$(tableName)' AND Reference Like '$(scrJoinType)';
  NEXT

  Drop Table JoinTypes;
ENDSUB
///$tab 02. QVDs
DIRECTORY [$(scrQVD_PATH_MD)];

//====Transaction tables====

TenderAward:
NoConcatenate
LOAD *
//     tender_id,
//     award_id,
//     award_date,
//     tender_date,
//     ocid,
//     tender_datemodifiedraw,
//     tender_datemodified,
//     tender_year,
//     tender_yearmonth,
//     tender_title,
//     tender_description,
//     tender_status,
//     tender_procurement_method,
//     tender_procurement_methoddetails,
//     tender_tender_period_end_date,
//     tender_tender_period_start_date,
//     tender_award_period_end_date,
//     tender_award_period_start_date,
//     tender_contract_period_end_date,
//     tender_contract_period_start_date,
//     tender_enquiry_period_end_date,
//     tender_enquiry_period_start_date,
//     tender_hasenquiries,
//     tender_procuring_entity_name_id,
//     planning_budget_id,
//     tender_techniques_hasframeworkagreement,
//     tender_techniques_frameworkagreement_method,
//     source_collection,
//     no_of_tenderers,
//     tender_relatedprocesses_uri,
//     tender_relatedprocesses_title,
//     tender_relatedprocesses_identifier,
//     tender_relatedprocesses_scheme,
//     tender_classification_id,
//     tender_classification_scheme,
//     tender_classification_description,
//     award_description,
//     award_title,
//     award_status,
//     award_unitofaccount,
//     tender_amount_aggregated,
//     tender_currency_aggregated,
//     award_amount_aggregated,
//     award_currency_aggregated,
//     tender_amount_USD,
//     tender_amount_CLF,
//     tender_amount_CLP,
//     award_amount_USD,
//     award_amount_CLF,
//     award_amount_CLP,
//     flag_savings_valid,
//     savings_USD,
//     savings_CLF,
//     savings_CLP,
//     days_tender_end_award_date,
//     days_tender_period_length,
//     days_tender_start_award_date
FROM [TenderAward.qvd]
(qvd);

Items:
NoConcatenate
LOAD *
//     tender_id,
//     item_unit_amount,
//     item_unit_currency,
//     item_unit_unitofaccount,
//     flag_no_award_item_currency_tender,
//     flag_no_award_item_amount_tender,
//     flag_no_award_currency_tender,
//     flag_no_award_amount_tender,
//     item_quantity,
//     item_description,
//     item_classification_id,
//     item_classification_uri,
//     item_classification_scheme,
//     item_unit_name,
//     flag_award_no_date
FROM [Items.qvd]
(qvd);

TendererSupplier:
NoConcatenate
LOAD *
//     tender_id,
//     flag_tenderer,
//     tender_tenderer_name_id,
//     flag_supplier,
//     award_supplier_name_id
FROM [TendererSupplier.qvd]
(qvd);

Contracts:
NoConcatenate
LOAD *
//     contract_id,
//     tender_id,
//     contract_description,
//     contract_title,
//     contract_status,
//     contract_amount,
//     contract_currency,
//     contract_unitofaccount,
//     contract_datecreated,
//     contract_period_enddate,
//     contract_period_startdate,
//     flag_no_award,
//     contract_amountgross,
//     contract_date,
//     days_contrarct_start_award_date
FROM [Contracts.qvd]
(qvd);

ContractMilestones:
NoConcatenate
LOAD *
//     contract_id,
//     milestones_id,
//     milestones_type,
//     milestones_status,
//     milestones_description,
//     milestones_datemet,
//     milestones_duedate,
//     milestones_datemodified,
//     milestones_associatedvalue
FROM [ContractMilestones.qvd]
(qvd);

//====Dictionary tables====

ProcuringEntity:
NoConcatenate
LOAD *
//     tender_procuring_entity_name_id,
//     procuringEntity_id,
//     procuringEntity_name,
//     procuringEntity_address_region,
//     procuringEntity_address_country,
//     procuringEntity_address_streetaddress,
//     procuringEntity_identifier_id,
//     procuringEntity_identifier_scheme,
//     procuringEntity_identifier_legalname,
//     procuringEntity_contactpoint_name,
//     procuringEntity_contactpoint_email,
//     procuringEntity_contactpoint_telephone,
//     procuringEntity_contactpoint_faxnumber,
//     procuringEntity_additionalidentifier_id,
//     procuringEntity_additionalidentifier_uri,
//     procuringEntity_additionalidentifier_scheme,
//     procuringEntity_additionalidentifier_legalname
FROM [ProcuringEntity.qvd]
(qvd);

Tenderers:
NoConcatenate
LOAD *
//     tenderer_name_id,
//     supplier_id,
//     supplier_name,
//     supplier_address_region,
//     supplier_address_country,
//     supplier_address_streetaddress,
//     supplier_identifier_id,
//     supplier_identifier_scheme,
//     supplier_identifier_legalname,
//     supplier_contactpoint_name,
//     supplier_contactpoint_email,
//     supplier_contactpoint_telephone,
//     supplier_contactpoint_faxnumber,
//     supplier_additionalidentifier_id,
//     supplier_additionalidentifier_uri,
//     supplier_additionalidentifier_scheme,
//     supplier_additionalidentifier_legalname
FROM [Tenderers.qvd]
(qvd);

Calendar:
NoConcatenate
LOAD *
//     tender_date,
//     "Calendar date",
//     "Year-Month",
//     "Month",
//     "Year-Quarter",
//     Quarter,
//     "Year",
//     "Año mes",
//     Mes,
//     "Del año pasado",
//     Trimestre,
//     Año
FROM [Calendar.qvd]
(qvd);

LanguageList:
NoConcatenate
LOAD *
//     _Language,
//     _Index
FROM [LanguageList.qvd]
(qvd);

CurrencyLabel:
NoConcatenate
LOAD *
//     _Currency,
//     _CurrencyIndex
FROM [CurrencyLabel.qvd]
(qvd);

ProcurementCategory:
NoConcatenate
LOAD *
FROM [ProcurementCategory.qvd]
(qvd);

ProcuringEntityArea:
NoConcatenate
LOAD *
FROM [ProcuringEntityArea.qvd]
(qvd);
///$tab 03. Rename Fields
RENAME FIELD 

//Tender 
tender_id_tender 					TO [Procedure ID]
,tender_date						TO [Procedure date]
,tender_title						TO [Procedure title]
,tender_description					TO [Procedure description]
,tender_status						TO [ProcedureStatus]
,no_of_tenderers					TO [Number of tenderers]
,no_of_suppliers					TO [Number of suppliers]
,tender_procurement_method			TO [ProcurementMethod]
,tender_procurement_methoddetails	TO [ProcurementMethodLocal]
,savings_USD						TO [Savings USD]
// ,savings_CLF						TO [Savings CLF]
,savings_CLP						TO [Savings CLP]
,days_tender_end_award_date			TO [Days from tender close to award decision]
,days_tender_period_length			TO [Tender period length]
,days_tender_start_award_date		TO [Days from tender start to award date]
,tender_amount_USD					TO [Procedure amount USD]
// ,tender_amount_CLF					TO [Procedure amount CLF]
,tender_amount_CLP					TO [Procedure amount CLP]
,overrun_USD						TO [Overrun USD]
// ,overrun_CLF						TO [Overrun CLF]
,overrun_CLP						TO [Overrun CLP]
,overrun_percent_USD				TO [Overrun percent USD]
// ,overrun_percent_CLF				TO [Overrun percent CLF]
,overrun_percent_CLP				TO [Overrun percent CLP]


//Award
,award_amount_USD					TO [Award amount USD]
// ,award_amount_CLF					TO [Award amount CLF]
,award_amount_CLP					TO [Award amount CLP]
,award_id							TO [Award ID]
,award_date							TO [Award date]
,award_description					TO [Award description]
,award_title						TO [Award title]
,award_status						TO [AwardStatus]

//Procuring entity
,procuringEntity_address_region 		TO [Buyer region]
// ,procuringEntity_address_region_en 		TO [Buyer region]
,procuringEntity_address_country 		TO [Buyer country]
,procuringEntity_address_streetaddress 	TO [Buyer address]
,procuringEntity_identifier_id 			TO [Buyer identifier]
,procuringEntity_identifier_scheme 		TO [Buyer scheme]
,procuringEntity_identifier_legalname 	TO [Buyer legal name]
,procuringEntity_contactpoint_email 	TO [Buyer email]
,procuringEntity_contactpoint_telephone TO [Buyer phone]
,procuringEntity_contactpoint_faxnumber TO [Buyer fax]
,procuringEntity_name_id_procuringEntity	TO [Buyer name ID]
// ,tender_procuring_entity_name_id		TO [Buyer name ID]
,procuringEntity_id_procuringEntity		TO [Buyer ID]
,procuringEntity_name					TO [Buyer name]

//tenderer
,supplier_address_region				TO [Bidder region]
,supplier_address_country 				TO [Bidder country]
,supplier_address_streetaddress 		TO [Bidder address]
,supplier_identifier_id 				TO [Bidder identifier]
,supplier_identifier_scheme 			TO [Bidder scheme]
,supplier_identifier_legalname 			TO [Bidder legal name]
,supplier_contactpoint_email 			TO [Bidder email]
,supplier_contactpoint_telephone 		TO [Bidder phone]
,supplier_contactpoint_faxnumber 		TO [Bidder fax]
,tender_tenderer_name_id_tenderer 		TO [Bidder name ID]
// ,tender_tenderer_name_id 				TO [Bidder name ID]
,tender_tenderer_id_tenderer 			TO [Bidder ID]
,supplier_name							TO [Bidder name]

//Contracts
,days_contrarct_start_award_date		TO [Days from award date to contract start date]
,contract_description 					TO [Contract description]
,contract_title 						TO [Contract title]
,contract_status 						TO [ContractStatus]
,contract_date		 					TO [Contract date]
,contract_period_enddate 				TO [Contract period end]
,contract_period_startdate 				TO [Contract period start]
,contract_id_contract					TO [Contract ID]
,contract_amount_USD 					TO [Contract amount USD]
// ,contract_amount_CLF 					TO [Contract amount CLF]
,contract_amount_CLP 					TO [Contract amount CLP]

//Contract Milestones
,milestones_id 							TO [Milestone ID]
,milestones_type 						TO [Milestone type]
,milestones_status 						TO [MilestoneStatus]
,milestones_description 				TO [Milestone description]
,milestones_datemet 					TO [Milestone date met]
,milestones_duedate 					TO [Milestone due date]

//Items
// ,item_id 								TO [Item ID]
,item_quantity 							TO [Item quantity]
,item_description 						TO [Item description]
,item_classification_id 				TO [Item classification ID]
,item_classification_uri 				TO [Item classification URI]
,item_classification_scheme 			TO [Item classification scheme]
,item_unit_name 						TO [Item unit name]
,item_unit_price_USD 					TO [Item unit price USD]
// ,item_unit_price_CLF 					TO [Item unit price CLF]
,item_unit_price_CLP 					TO [Item unit price CLP]
,item_classification_name_id			TO [Item classification]
,item_category_name_id					TO [Item procurement category]
,item_id_competitive					TO [Item ID]
;
///$tab 04. Multilang
Directory [$(scrDIC_QVD_PATH)];

//Field names for multilingual dictionaries
FieldNamesTable:
NoConcatenate
// LOAD
//     "Index",
//     En,
//     Ro
// FROM MultilangFieldNames.qvd (qvd);
///////////For Dev Only!!!///////////////
Load
    F1 as Index,
    F3 as En,
    F4 as Es
FROM [$(scrURLConnectionName)]
(html, utf8, embedded labels, table is @1, 
url is 'https://docs.google.com/spreadsheets/u/1/d/$(scrMultilangURL)/gviz/tq?tqx=out:html&tq&gid=$(scrMultilangFields)')
Where F1 <> 'Index' AND Len(F1) > 0;
//////////////////////////

//Names and labels for internal KPIs and dimensions
InnerDictionaries:
// Load
//     "Index",
//     Reference,
//     En,
//     Ro,
//     attributeOf
// From MultilangDictionaries.qvd (qvd);
///////////For Dev Only!!!///////////////
Load
    F1 as Index,
    F2 as Reference,
    F3 as En,
    F4 as Es,
    F6 as attributeOf
From [$(scrURLConnectionName)]
(html, utf8, embedded labels, table is @1, 
url is 'https://docs.google.com/spreadsheets/u/1/d/$(scrMultilangURL)/gviz/tq?tqx=out:html&tq&gid=$(scrMultiLangInner)')
Where F1 <> 'Index' AND Len(F1) > 0;
//////////////////////////

//Label translations
Set Verbatim = 1;

Translations:
NoConcatenate
// Load     
// 	"Index",
//     En,
//     Ro
// From MultilangLabels.qvd (qvd);
///////////For Dev Only!!!///////////////
Load
    F1 as "Index",
    F3 as En,
    F4 as Es
FROM [$(scrURLConnectionName)]
(html, utf8, embedded labels, table is @1, 
url is 'https://docs.google.com/spreadsheets/u/1/d/$(scrMultilangURL)/gviz/tq?tqx=out:html&tq&gid=$(scrMultilangTransl)')
Where F1 <> 'Index' AND Len(F1) > 0;
//////////////////////////

Language:
CROSSTABLE (Language, Translation) LOAD 
	 Index, 
     En,
     Es
Resident Translations;

// LanguageList:
// Load Distinct
// 	 Language as _Language
// Resident Language
// Where Language > 0;

// Left Join(LanguageList)
// Load
//     Index as [_Language],
//     En as [_Index]
// Resident InnerDictionaries
// Where attributeOf like 'LanguageList';

// DROP TABLE Translations;

// Variables for UI localizing
Let T = replace('_T_$1_@(=FirstSortedValue(distinct _Language, _Index))', '@', '$');

LET scrTranslationsRows = NoOfRows('Language');

FOR i = 0 to scrTranslationsRows - 1 
	Let _scrTranslationId = Peek('Index', i, 'Language');
	Let _scrTranslation = Peek('Translation', i, 'Language');
	Let _scrTrLanguage = Peek('Language', i, 'Language');
	
	Let _scrTrName = '_T_$(_scrTranslationId)_$(_scrTrLanguage)';
	Let $(_scrTrName) = '$(_scrTranslation)';
	
	Let _scrTranslationId =;
	Let _scrTranslation =;
	Let _scrTrLanguage =;
	Let _scrTrName =;
NEXT

Drop Table Language;

// Currency selection
Let scrCurrency = replace('@(=FirstSortedValue(_Currency, _CurrencyIndex))', '@', '$');

//Sheet and object descriptions 
HelpTranslation:
NoConcatenate
// Load     
// 	"Index",
//     En,
//     Ro
// From MultilangHelp.qvd (qvd);
///////////For Dev Only!!!///////////////
Load
    F1 as "Index",
    F3 as En,
    F4 as Es
FROM [$(scrURLConnectionName)]
(html, utf8, embedded labels, table is @1, 
url is 'https://docs.google.com/spreadsheets/u/1/d/$(scrMultilangURL)/gviz/tq?tqx=out:html&tq&gid=$(scrMultilangHelp)')
Where F1 <> 'Index' AND Len(F1) > 0;
//////////////////////////

LanguageHelp:
CROSSTABLE (Language, Translation) 
LOAD 
	 Index, 
     En,
     Es
Resident HelpTranslation;

// Переменные для локализации хэлпов
LET H = replace('_H_$1_@(=FirstSortedValue(_Language, _Index))', '@', '$');

LET scrTranslationsRows = NoOfRows('LanguageHelp');

FOR i = 0 TO scrTranslationsRows - 1 
	Let _scrTranslationId = Peek('Index', i, 'LanguageHelp');
	Let _scrTranslation = Peek('Translation', i, 'LanguageHelp');
	Let _scrTrLanguage = Peek('Language', i, 'LanguageHelp');
	
	Let _scrTrName = '_H_$(_scrTranslationId)_$(_scrTrLanguage)';
	Let $(_scrTrName) = '$(_scrTranslation)';
	
	Let _scrTranslationId =;
	Let _scrTranslation =;
	Let _scrTrLanguage =;
	Let _scrTrName =;
NEXT

DROP Tables LanguageHelp, HelpTranslation;


///$tab 05. Variables
//====Fields===

//Tender
Set fldTenderId 				= '[Procedure ID]';
Set fldTenderTitle 				= '[Procedure title]';
Set fldTenderDescription 		= '[Procedure description]';
Set fldNoOfBidder 				= '[Number of tenderers]';
Set fldNoOfSuppliers 			= '[Number of suppliers]';
Set fldProcurementMethod 		= 'ProcurementMethod';
Set fldProcurementMethodLocal 	= 'ProcurementMethodLocal';
Set fldTenderAmount 			= =replace('[Procedure amount @(scrCurrency)]', '@', '$');
Set fldTenderStatus 			= 'ProcedureStatus';
Set fldTenderCloseAward 		= '[Days from tender close to award decision]';
Set fldLengthOfTenderPeriod 	= '[Tender period length]';
Set fldAwardTenderStart 		= '[Days from tender start to award date]';
Set fldSavings					= =replace('[Savings @(scrCurrency)]', '@', '$');
Set fldTenderDate				= '[Calendar date]';
SET fldCompetitiveness			= 'flag_tender_competitiveness';
SET fldOverrun					= =replace('[Overrun @(scrCurrency)]', '@', '$');
SET fldOverrunPercent			= =replace('[Overrun percent @(scrCurrency)]', '@', '$');

//Award
Set fldAwardId 					= '[Award ID]';
Set fldAwardStatus				= 'AwardStatus';
Set fldAwardAmount 				= =replace('[Award amount @(scrCurrency)]', '@', '$');
Set fldAwardDate				= '[Award date]';
// Set fldAwardedFlag 				= 'AwardedFlag';
Set fldBidId					= 'bid_id';

//Procuring Entity
Set fldProcuringEntityId 		= '[Buyer ID]';
Set fldProcuringEntity 			= '[Buyer name ID]';
Set fldProcuringEntityName 		= '[Buyer name]';
Set fldProcuringEntityRegion 	= '[Buyer region]';
Set fldProcuringEntityAddress 	= '[Buyer address]';
Set fldProcuringEntityCountry 	= '[Buyer country]';
Set fldProcuringEntityEmail		= '[Buyer email]';
Set fldProcuringEntityPhone		= '[Buyer phone]';
Set fldProcuringEntityFax		= '[Buyer fax]';

Set fldProcuringEntityGUID		= 'tender_procuring_entity_id';

//Bidder
Set fldTendererId				= '[Bidder ID]';
Set fldTenderer 				= '[Bidder name ID]';
Set fldTendererName 			= '[Bidder name]';
Set fldTendererRegion 			= '[Bidder region]';
Set fldTendererAddress 			= '[Bidder address]';
Set fldTendererCountry 			= '[Bidder country]';
Set fldTendererEmail			= '[Bidder email]';
Set fldTendererPhone			= '[Bidder phone]';
Set fldTendererFax				= '[Bidder fax]';
Set fldIsSupplier 				= 'flag_supplier';
Set fldIsTenderer 				= 'flag_tenderer';

Set fldTendererGUID				= 'tender_tenderer_id';


//Contracts
SET fldContractId				= '[Contract ID]';
SET fldContractDate				= '[Contract date]';
SET fldContractStatus			= 'ContractStatus';
SET fldContractAmount			= =replace('[Contract amount @(scrCurrency)]', '@', '$');

SET fldShareMilestonesOnTime	= 'share_milestones_on_time';
SET fldMilestonesOnTime			= 'flag_contract_on_time';
SET fldMilestonesPresent		= 'flag_present_milestones';


//Items
SET fldItemId					= 'item_id';
Set fldCPVclassification 		= '[Item classification]';
SET fldItemProcCategory			= '[Item procurement category]';
Set fldTenderItemUnit 			= '[Item unit name]';
Set fldItemQty 					= '[Item quantity]';
// Set fldIsSingleItem = 'SingleItemFlag';
Set fldUnitPrice 				= =replace('[Item unit price @(scrCurrency)]', '@', '$');
SET fldItemAwarded				= 'flag_item_awarded';
SET fldItemIdOrig				= '[Item ID]';

//Current language
Let expLan = Replace('@(=_Language)', '@', '$');

//====Flags====

//Set Analysis
Set scrPARAMS 			= (=replace('$1', ';', ',')); 

Set saPMOpen 			= '$(fldProcurementMethod)={open}';
Set saPMDirect 			= '$(fldProcurementMethod)={direct}'; 
Set saPMSelective		= '$(fldProcurementMethod)={selective}';
SET saPMFA				= '$(fldProcurementMethod)={frameworkagreement}';
SET saGPMDirect			= '$(fldCompetitiveness)={1}';
SET saGPMOpen			= '$(fldCompetitiveness)={0}';
// Set saIsBid = '$(fldBidAwardType)={1}';
// Set saIsAward = '$(fldBidAwardType)={2}';
Set saExclCancUnsucAward = '$(fldAwardStatus)-={unsuccessful,cancelled}';
Set saExclCancUnsucAwardA = '$(fldAwardStatus)-={unsuccessful;cancelled}';
// Set saActiveAward = '$(fldAwardStatus)={active}';
// Set saPendingAward = '$(fldAwardStatus)={pending}';
// Set saActiveDetailsAward = '$(fldAwardStatusDetails)={active}';
Set saAwarded 			= '$(fldAwardId)=P()';
// Set saUnsuccessfulDetailsAward = '$(fldAwardStatusDetails)={unsuccessful}';
Set saIsTenderer	= '$(fldIsTenderer)={1}';
Set saIsSupplier 	= '$(fldIsSupplier)={1}';
Set saIsSingleBid 	= '$(fldNoOfBidder)={1}';
Set saCancTender 	= '$(fldTenderStatus)={cancelled}';
// Set saIsSingleItem = '$(fldIsSingleItem)={1}';
// Set saIsComplaint = '$(fldIsComplaint)={1}';
SET saMilestones	= '$(fldMilestonesPresent)={1}';
SET saMilestonesOnTime	= '$(fldMilestonesOnTime)={1}';
SET saItemAwarded		= '$(fldItemAwarded)={1}';

//====KPIs====
Set scrNO_OF_PROCEDURES_ 		= 'COUNT({<$'&scrPARAMS&'>} Distinct $(fldTenderId)'&')';
Let expNoOfProcedures 			= $(scrNO_OF_PROCEDURES_);

Set scrNO_OF_PROC_ENTITIES_ 	= 'COUNT({<$'&scrPARAMS&'>} Distinct $(fldProcuringEntityGUID)'&')';
Let expNoOfProcuringEntities 	= $(scrNO_OF_PROC_ENTITIES_);

Set scrNO_OF_SUPPLIERS_ 		= 'COUNT({<$'&scrPARAMS&',$(saIsSupplier)>} Distinct $(fldTendererGUID)'&')';
Let expNoOfSuppliers 			= $(scrNO_OF_SUPPLIERS_);

Set scrNO_OF_BIDDERS_ 			= 'COUNT({<$'&scrPARAMS&',$(saIsTenderer)>} Distinct $(fldTendererGUID)'&')';
Let expNoOfBidders 				= $(scrNO_OF_BIDDERS_);

Set scrNO_OF_PARTICIPANTS_ 		= 'COUNT({<$'&scrPARAMS&'>} Distinct $(fldTendererGUID)'&')';
Let expNoOfParticipants 		= $(scrNO_OF_PARTICIPANTS_);

Set scrAWARDED_VALUE_ 			= 'SUM({<$'&scrPARAMS&',$(saIsSupplier),$(saExclCancUnsucAward)>} $'&'($(fldAwardAmount)'&'))';
Let expAwardedValue 			= $(scrAWARDED_VALUE_);

Set scrAWARDED_VALUE_ALL_ 		= 'SUM({<$'&scrPARAMS&',$(saIsSupplier)>} $'&'($(fldAwardAmount)'&'))';
Let expAwardedValueAll 			= $(scrAWARDED_VALUE_ALL_);

Set scrAWARDED_VALUE_TOTAL_ 	= 'SUM({<$'&scrPARAMS&',$(saIsSupplier),$(saExclCancUnsucAward)>} Total $'&'($(fldAwardAmount)'&'))';
Let expAwardedValueTotal 		= $(scrAWARDED_VALUE_TOTAL_);

Set scrEXPECTED_VALUE_ 			= 'SUM({<$'&scrPARAMS&'>} $'&'($(fldTenderAmount)'&'))';
Let expExpectedValue 			= $(scrEXPECTED_VALUE_);

Set scrSAVINGS_VALUE_ 			= 'SUM({<$'&scrPARAMS&',$(saExclCancUnsucAward)>} $'&'($(fldSavings)'&'))';
Let expSavings		 			= $(scrSAVINGS_VALUE_);

Set scrOVERRUN_VALUE_ 			= 'SUM({<$'&scrPARAMS&',$(saExclCancUnsucAward)>} $'&'($(fldOverrun)'&'))';
Let expOverrun		 			= $(scrOVERRUN_VALUE_);

Set scrOVERRUN_MEAN_VALUE_ 		= 'AVG({<$'&scrPARAMS&',$(saExclCancUnsucAward)>} $'&'($(fldOverrunPercent)'&'))';
Let expOverrunMeanPercent		 = $(scrOVERRUN_MEAN_VALUE_);

Set scrCONTRACTED_VALUE_ 		= 'SUM({<$'&scrPARAMS&'>} $'&'($(fldContractAmount)'&'))';
Let expContractedValue 			= $(scrCONTRACTED_VALUE_);

Set scrNO_OF_CONTRACTS_ 		= 'COUNT({<$'&scrPARAMS&'>} Distinct $(fldContractId)'&')';
Let expNoOfContracts 			= $(scrNO_OF_CONTRACTS_);

Set scrNO_OF_AWARDS_ 			= 'COUNT({<$'&scrPARAMS&',$(saIsSupplier),$(saExclCancUnsucAward)>} Distinct $(fldAwardId)'&')';
Let expNoOfAwards 				= $(scrNO_OF_AWARDS_);

Set scrNO_OF_BIDS_ 				= 'COUNT({<$'&scrPARAMS&'>} $(fldBidId)'&')';
Let expNoOfBids 				= $(scrNO_OF_BIDS_);

Set scrNO_OF_ITEMS_ 	 		= 'COUNT({<$'&scrPARAMS&'>} Distinct $(fldItemId)'&')';
Let expNoOfItems 				= $(scrNO_OF_ITEMS_);

Set scrTENDER_CLOSE_AWARD_ 		= 'AVG({<$'&scrPARAMS&'>} $(fldTenderCloseAward)'&')';
Let expTenderCloseAward 		= $(scrTENDER_CLOSE_AWARD_);

Set scrLENGTH_OF_TENDER_PERIOD_ = 'AVG({<$'&scrPARAMS&'>} $(fldLengthOfTenderPeriod)'&')';
Let expLengthOfTenderPeriod 	= $(scrLENGTH_OF_TENDER_PERIOD_);

Set scrAWARD_TENDER_START_ 		= 'AVG({<$'&scrPARAMS&'>} $(fldAwardTenderStart)'&')';
Let expAwardTenderStart 		= $(scrAWARD_TENDER_START_);

Set scrAVG_UNIT_PRICE_ 			= 'AVG({<$'&scrPARAMS&'>} $'&'($(fldUnitPrice)'&'))';
Let expAvgUnitPrice 			= $(scrAVG_UNIT_PRICE_);

Set scrMEDIAN_UNIT_PRICE_ 		= 'MEDIAN({<$'&scrPARAMS&'>} $'&'($(fldUnitPrice)'&'))';
Let expMedianUnitPrice 			= $(scrMEDIAN_UNIT_PRICE_);

Set scrMIN_UNIT_PRICE_ 			= 'MIN({<$'&scrPARAMS&'>} $'&'($(fldUnitPrice)'&'))';
Let expMinUnitPrice 			= $(scrMIN_UNIT_PRICE_);

Set scrMAX_UNIT_PRICE_ 			= 'MAX({<$'&scrPARAMS&'>} $'&'($(fldUnitPrice)'&'))';
Let expMaxUnitPrice 			= $(scrMAX_UNIT_PRICE_);

Set scrITEM_UNIT_PRICE_ 		= 'SUM({<$'&scrPARAMS&'>} $'&'($(fldUnitPrice)'&'))';
Let expItemUnitPrice 			= $(scrITEM_UNIT_PRICE_);

Set scrMEAN_NO_OF_BIDDERS_PER_LOT_ 		= 'AVG({<$'&scrPARAMS&'>} $(fldNoOfBidder)'&')';
Let expMeanNoOfBiddersPerLot 			= $(scrMEAN_NO_OF_BIDDERS_PER_LOT_);

Set scrMEDIAN_NO_OF_BIDDERS_PER_LOT_ 	= 'MEDIAN({<$'&scrPARAMS&'>} $(fldNoOfBidder)'&')';
Let expMedianNoOfBiddersPerLot 			= $(scrMEDIAN_NO_OF_BIDDERS_PER_LOT_);

Set scrAVG_MILESTONES_ON_TIME 	= 'AVG({<$'&scrPARAMS&'>} $(fldShareMilestonesOnTime)'&')';
Let expAvgMilestonesOnTime		= $(scrAVG_MILESTONES_ON_TIME);

//====Formatting interface====

Set scrSize = Alt(Div(Log10(Fabs($1)),3), 0);
Let fmtNumConvert = Replace('Dual(Pick($(scrSize($1))+1, 
NUM(RangeSum($1,Left($2,2)), ''#,##0''), 
NUM($1/1000, ''#,##0.00@(=@(T(fmtK)))'', ''.'', '',''), 
NUM($1/1000000, ''#,##0.00@(=@(T(fmtMln)))'', ''.'', '',''), 
NUM($1/1000000000, ''#,##0.00@(=@(T(fmtBln)))'', ''.'', '',''), 
NUM($1/1000000000000, ''#,##0.00@(=@(T(fmtTrln)))'', ''.'', '',''),
NUM($1/1000000000000000, ''#,##0.00@(=@(T(fmtQdrl)))'', ''.'', '','')
),$1)', '@', '$');
///$tab 06. Interface Tables
//Drop down KPI list (General for all sheets)

KPIList:
NoConcatenate
Load
	 _KPINo, 
     replace(_Exp, '@', '$') as _Exp
     ;
Load * Inline [
_KPINo,	_Exp
1,		@(expNoOfProcedures)	
2,		@(expExpectedValue)
3,		@(expNoOfAwards)
4,		@(expAwardedValue)
];

CALL translationsJoinInners('KPIList', '_KPINo');

//Translations of dictionaries

CALL translationsNewTable('ProcedureStatus', 'TenderAward', 'ProcedureStatus');
CALL translationsNewTable('ContractStatus', 'Contracts', 'ContractStatus');
CALL translationsNewTable('AwardStatus', 'TenderAward', 'AwardStatus');
CALL translationsNewTable('ProcurementMethod', 'TenderAward', 'ProcurementMethod');
CALL translationsNewTable('ProcurementMethodLocal', 'TenderAward', 'ProcurementMethodLocal');
CALL translationsNewTable('NonCompetitiveSupplier', 'Tenderers', 'flag_bidder_non_competitive');
CALL translationsNewTable('NonCompetitiveProcuringEntity', 'ProcuringEntity', 'flag_procuring_entity_non_competitive');


Drop Tables FieldNamesTable, InnerDictionaries;
///$tab 07. Benchmarking
$(Must_Include=lib://OCDSint/Benchmarking/UpdateSection/Chile.txt);
///$tab Exit
Exit script;