SET scrLOCAL_BENCH_PATH = 'lib://OCDSint/Benchmarking/';

details:
Load IDLot
	,[Tender ID]
    ,TenderStatus
	,ProcurementMethod
    ,IDProcuringEntity
    ,[Number of bidders]
    ,[Days from tender close to award decision] as DaysFromCloseToAward
    ,[Length of tender period] as [TenderPeriod]
    ,Year([Date]) as [BM_Year]
    ,Month(Date) as [Month]
Resident Lot;

LET details_NoOfRows = NoOfRows('details');
Trace details_NoOfRows >> '$(details_NoOfRows)';

// Left Join (details)
awarded:
Load distinct IDLot
	,[Award amountUSD]
    ,IDBidder as awarded_supplier
    ,IDAward
    ,AwardedFlag
Resident Bid
Where AwardedFlag = 1;

Left Join (awarded)
Load IDLot
	,[Lot amountUSD]
Resident Lot;

Left Join (details)
Load *
Resident awarded;

Drop table awarded;

LET details_NoOfRows = NoOfRows('details');
Trace details_NoOfRows >> '$(details_NoOfRows)';

// Tender CPV Division
item_cpv_div:
Load IDLot
	,Left(SubField([Tender item CPV classification],' - ',2),2) as item_cpv_div
Resident TenderItem;

Left Join (item_cpv_div)
Load IDLot
	,[Tender ID]
Resident Lot;

tender_cpv_div:
Load [Tender ID]
	,Concat(distinct item_cpv_div,',') as tender_cpv_div
Resident item_cpv_div
Group by [Tender ID];

Drop table item_cpv_div;

Left Join (details)
Load [Tender ID]
	,IF(SubStringCount(tender_cpv_div,',')>0,0,tender_cpv_div) as CPV2
Resident tender_cpv_div;

Drop table tender_cpv_div;

data:
Load [BM_Year]
	,[Month]
    ,TenderStatus
    ,ProcurementMethod
    ,CPV2
    ,Count(Distinct [Tender ID]) as M06 // Number of procedures
    ,SUM([Award amountUSD]) as M05 // Awarded value
    ,SUM([Award amountUSD]) as M02 // Contracted value
    ,Count(Distinct IF(AwardedFlag=1,IDLot)) as C02 // awarded lots count
    ,Count(Distinct IF([Number of bidders]=1,IDLot)) as C04_1
    ,Count(Distinct IDLot) as C04_2
    ,Sum(IF([Number of bidders]=1,[Award amountUSD])) as C05 // % Singlebid Award amount: C05/M05
    ,Sum([Number of bidders]) as C06_SUM
    ,Count([Number of bidders]) as C06_COUNT
    ,Median([Number of bidders]) as C07
    ,SUM(DaysFromCloseToAward) as E04_SUM
    ,Count(DaysFromCloseToAward) as E04_COUNT
    ,SUM([TenderPeriod]) as E06_SUM
    ,Count([TenderPeriod]) as E06_COUNT
    ,Count(Distinct IF(AwardedFlag=1 and ([Award amountUSD]>[Lot amountUSD]),IDAward)) as V03_1
    ,Count(Distinct IDAward) as V03_2
Resident details
Group by [BM_Year],[Month],TenderStatus,ProcurementMethod,CPV2;

Concatenate (data)
Load [BM_Year]
    ,Count(Distinct IDProcuringEntity) as M07 // Number of buyers
    ,Count(Distinct awarded_supplier) as M03 // Number of suppliers
Resident details
Group By [BM_Year];

Drop table details;

FOR Each scrBM_Year in FieldValueList('BM_Year')

BM_$(scrBM_Year):
Load 'MD' as country
	,'MD' as country_tmp
	,*
Resident data
Where [BM_Year] = $(scrBM_Year);

IF NOT IsNull( FileSize('$(scrLOCAL_BENCH_PATH)BM_$(scrBM_Year).qvd')) THEN

Concatenate (BM_$(scrBM_Year))
Load *
FROM [$(scrLOCAL_BENCH_PATH)BM_$(scrBM_Year).qvd] (qvd)
Where NOT Exists(country_tmp,country);

ENDIF;

Drop field country_tmp;

Store BM_$(scrBM_Year) into [$(scrLOCAL_BENCH_PATH)BM_$(scrBM_Year).qvd] (qvd);
Drop table BM_$(scrBM_Year);

NEXT

Drop table data;